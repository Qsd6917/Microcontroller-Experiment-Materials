;*******************************************************************
;【提  示】下列程序是在系统时钟为12MHZ（或11.0592MHZ），即NOP指令为1微秒左右。
;（1）带有内部单元地址的多字节写操作子程序 WRNBYT
;*******************************************************************
;通用的I2C通讯子程序（多字节写操作）
;入口参数R7字节数,R0源数据块首地址
; R2从器件内部子地址;R3外围器件地址（写）
;相关子程序WWRBYT、STOP、CACK、STA
;*******************************************************************	
WRNBYT:	PUSH	PSW		
			PUSH	ACC				
WRADD:	MOV	A,R3		;取外围器件地地址（包含r/w=0）	
			LCALL	STA			;发送起始信号S  
			LCALL	WRBYT		;发送外围地址
		LCALL	CACK		;检测外围器件的应答信号
		JB		F0,WRADD	;如果应答不正确返回重来
		MOV	A,R2
		LCALL	WRBYT		;发送内部寄存器首地址
		LCALL	CACK		;检测外围器件的应答信号
		JB		F0,WRADD	;如果应答不正确返回重来 	
WRDA:	MOV	A,@R0
		LCALL	WRBYT		;发送外围地址
		LCALL	CACK		;检测外围器件的应答信号
		JB		F0,WRADD	;如果应答不正确返回重来
		INC		R0
		DJNZ  	R7,WRDA
		LCALL	STOP 	
		POP	ACC
		POP	PSW
		RET 	       
;*******************************************************************
;（2）带有内部单元地址的多字节读操作子程序RDNBYT 
;*******************************************************************
;通用的I2C通讯子程序（多字节读操作）
;入口参数R7字节数；
;R0目标数据块首地址；R2从器件内部子地址；
;R3器件地址（写）；R4器件地址（读）
;相关子程序WWRBYT、STOP、CACK、STA、MNACK 
;*******************************************************************	
RDNBYT: PUSH	PSW	
		PUSH	ACC	
RDADD1:LCALL	STA 
		MOV	A,R3		;取器件地址（写）
		LCALL	WRBYT		;发送外围地址
		LCALL	CACK		;检测外围器件的应答信号
		JB		F0,RDADD1	;如果应答不正确返回重来
		MOV	A,R2		;取内部地址	
		LCALL	WRBYT		;发送外围地址
		LCALL	CACK		;检测外围器件的应答信号
		JB		F0,RDADD1	;如果应答不正确返回重来	
		LCALL	STA
		MOV	A,R4		;取器件地址（读）
		LCALL	WRBYT		;发送外围地址
		LCALL	CACK		;检测外围器件的应答信号
		JB		F0,RDADD1	;如果应答不正确返回重来
RDN:	LCALL	RDBYT 	
		MOV	@R0,A
		DJNZ	R7,ACK
		LCALL	MNACK
		LCALL	STOP	
		POP	ACC
		POP	PSW
		RET
ACK:	LCALL	MACK
		INC		R0
		SJMP	RDN 
;**********************************************************************
;（3）I2C各个信号子程序
;**********************************************************************
;		启动信号子程序S 
;**********************************************************************
STA:	SETB	SDA		;启动信号S
		SETB	SCL
		NOP				;产生4.7US延时
		NOP
		NOP
		NOP
		NOP	
		CLR	SDA
		NOP				;产生4.7US延时
		NOP
		NOP
		NOP
		NOP 	
		CLR	SCL
		RET 
;**********************************************************************
;		停止信号子程序P 
;**********************************************************************
STOP:	CLR	SDA 	;停止信号P
		SETB	SCL
		NOP				;产生4.7US延时
		NOP
		NOP
		NOP
		NOP	
		SETB	SDA
		NOP				;产生4.7US延时
		NOP
		NOP
		NOP
		NOP
		SETB	SCL			;释放总线
		SETB	SDA	
		RET 
;**********************************************************************
;		应答信号子程序   MACK
;**********************************************************************
MACK:	CLR	SDA	;发送应答信号ACK
		SETB	SCL
		NOP			;产生4.7US延时
		NOP
		NOP
		NOP
		NOP
		CLR	SCL
		SETB	SDA
		RET
;**********************************************************************
;		非应答法信号子程序MNACK
;**********************************************************************
MNACK:	SETB	SDA		;发送非应答信号NACK
		SETB	SCL
		NOP				;产生4.7US延时
		NOP
		NOP
		NOP
		NOP
		CLR	SCL
		CLR	SDA
		RET
;**********************************************************************
;		应答检测子程序CACK
;**********************************************************************
CACK:	SETB	SDA		;应答位检测子程序
		SETB	SCL 
		CLR	F0
		MOV	C,SDA		;采样SDA
		JNC		CEND		;应答正确时转CEND
		SETB	F0			;应答错误时F0置一
CEND:	CLR	SCL
		RET
;**********************************************************************
;		发送一个字节子程序WRBYT
;**********************************************************************
WRBYT:	PUSH	06H
MOV	R6,#08H		;发送一个字节子程序 
WLP:	RLC	A 			;入口参数A
		MOV	SDA,C
		SETB	SCL
		NOP				;产生4.7US延时
		NOP
		NOP
		NOP
		NOP
		JNB		SCL,$
		CLR	SCL
		DJNZ	R6,WLP
		POP	06H
		RET
;**********************************************************************
;		接收一个字节子程序RDBYT 
;**********************************************************************
RDBYT: PUSH	06H
		MOV	R6,#08H		;接收一个字节子程序，出口参数R2
RLP:	SETB	SDA
		SETB	SCL
		JNB		SCL,$
		MOV	C,SDA
		MOV	A,R2
		RLC	A
		MOV	R2,A
		CLR	SCL
		DJNZ	R6,RLP		
		POP	06H
		RET  
;**********************************************************************
